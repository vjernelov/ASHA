archetype (adl_version=1.4; uid=0dcffeb4-d693-4de4-878c-bdaa9b93f853)
	openEHR-EHR-CLUSTER.pharmacogenetic_test_result.v0

concept
	[at0000]

language
	original_language = <[ISO_639-1::en]>

description
	original_author = <
		["date"] = <"2023-05-22">
		["name"] = <"Ian McNicoll">
		["organisation"] = <"freshEHR Clinical Informatics">
		["email"] = <"ian@freshEHR.com">
	>
	lifecycle_state = <"in_development">
	other_contributors = <"John McDermott, University of Manchester","Videha Sharma, University of Manchester","Jessica Keen, North West Genomic Medicine Service Alliance">
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record findings from pharmacogenetic testing of a single gene, used to predict how an individual may respond to specific medications.">
			keywords = <"pharmacogenetics, laboratory, enzyme, medication, pgx, CPIC, genomics, pharmacogenomics, genotype", ...>
			copyright = <"© openEHR Foundation">
			use = <"Use to record findings from pharmacogenetic testing of a single gene, used to predict how an individual may respond to specific medications.

This archetype has been designed so that one or more instances of this archetype may be nested within the 'Analyte result detail' SLOT in the CLUSTER.laboratory_test_analyte archetype, carried inside the 'Test result' SLOT in the OBSERVATION.laboratory_test_result archetype, to be consistent with the existing approach to laboratory modelling.">
			misuse = <"Not to be used to record information about specific genomic variants. Use one or more of the Genomics variant family of archetypes for this purpose.

Not to be used to record therapeutic recommendations that are the conclusions or interpretations based on each Pharmacogenetic test result.">
		>
	>
	other_details = <
		["licence"] = <"This work is licensed under the Creative Commons Attribution-ShareAlike 4.0 International License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/4.0/.">
		["custodian_organisation"] = <"Apperta UK">
		["references"] = <"Caudle KE, Dunnenberger HM, Freimuth RR, Peterson JF, Burlison JD, Whirl-Carrillo M, Scott SA, Rehm HL, Williams MS, Klein TE, Relling MV, Hoffman JM. Standardizing terms for clinical pharmacogenetic test results: consensus terms from the Clinical Pharmacogenetics Implementation Consortium (CPIC). Genet Med. 2017 Feb;19(2):215-223. doi: 10.1038/gim.2016.87. Epub 2016 Jul 21. PMID: 27441996; PMCID: PMC5253119.

Goar W, Babb L, Chamala S, Cline M, Freimuth RR, Hart RK, Kuzma K, Lee J, Nelson T, Prlić A, Riehle K, Smith A, Stahl K, Yates AD, Rehm HL, Wagner AH. Development and application of a computable genotype model in the GA4GH Variation Representation Specification. Pac Symp Biocomput. 2023;28:383-394. PMID: 36540993; PMCID: PMC9782714.

Health Level 7 International [Internet]. Genomics Reporting Implementation Guide [cited 2024 Sep 3]. Available from: https://hl7.org/fhir/uv/genomics-reporting/.

Dolin RH, Boxwala A, Shalaby J. A Pharmacogenomics Clinical Decision Support Service Based on FHIR and CDS Hooks. Methods Inf Med. 2018 Dec;57(S 02):e115-e123. doi: 10.1055/s-0038-1676466. Epub 2019 Jan 3. PMID: 30605914.

Professional Record Standards Body [Internet]. Using pharmacogenomic information in clinical practice [cited 2024 Sep 3]. Available from: https://theprsb.org/projects/geneticsandmedicines/.

McDermott JH, Wright S, Sharma V, Newman WG, Payne K, Wilson P. Characterizing pharmacogenetic programs using the consolidated framework for implementation research: A structured scoping review. Front Med (Lausanne). 2022 Aug 18;9:945352. doi: 10.3389/fmed.2022.945352. PMID: 36059837; PMCID: PMC9433561.

Roosan D, Hwang A, Law AV, Chok J, Roosan MR. The inclusion of health data standards in the implementation of pharmacogenomics systems: a scoping review. Pharmacogenomics. 2020 Nov;21(16):1191-1202. doi: 10.2217/pgs-2020-0066. Epub 2020 Oct 30. PMID: 33124487.">
		["current_contact"] = <"Ian McNicoll (ian@freshehr.com>">
		["original_namespace"] = <"org.openehr">
		["original_publisher"] = <"openEHR International">
		["custodian_namespace"] = <"uk.org.clinicalmodels">
		["MD5-CAM-1.0.1"] = <"9E6A3F8B6DA7E458937A0BE8172E18FA">
		["build_uid"] = <"fde0aedf-941b-4ba8-addc-992eb85fcb2a">
		["ip_acknowledgements"] = <"This artefact includes content from SNOMED Clinical Terms® (SNOMED CT®) which is copyrighted material of the International Health Terminology Standards Development Organisation (IHTSDO). Where an implementation of this artefact makes use of SNOMED CT content, the implementer must have the appropriate SNOMED CT Affiliate license - for more information contact https://www.snomed.org/snomed-ct/get-snomed or info@snomed.org.">
		["revision"] = <"0.0.1-alpha">
	>

definition
	CLUSTER[at0000] matches {    -- Pharmacogenetic test result
		items cardinality matches {1..*; unordered} matches {
			ELEMENT[at0003] matches {    -- Gene symbol
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0004] occurrences matches {0..1} matches {    -- Diplotype
				value matches {
					DV_TEXT matches {*}
				}
			}
			ELEMENT[at0052] occurrences matches {0..1} matches {    -- Phenotype
				value matches {
					DV_CODED_TEXT matches {
						defining_code matches {
							[local::
							at0053,    -- Normal metaboliser
							at0054,    -- Rapid metaboliser
							at0055,    -- Ultrarapid metaboliser
							at0056,    -- Intermediate metaboliser
							at0057,    -- Poor metaboliser
							at0087,    -- Increased function
							at0088,    -- Decreased function
							at0089,    -- Normal function
							at0090,    -- Poor function
							at0091,    -- Negative
							at0092,    -- Positive
							at0093,    -- Normal risk of aminoglycoside-induced hearing loss
							at0094,    -- Increased risk of aminoglycoside-induced hearing loss
							at0095]    -- Uncertain risk of aminoglycoside-induced hearing loss
						}
					}
					DV_TEXT matches {*}
				}
			}
			CLUSTER[at0084] occurrences matches {0..*} matches {    -- Individual activity value
				items cardinality matches {1..*; unordered} matches {
					ELEMENT[at0086] occurrences matches {0..1} matches {    -- Allele haplotype
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0085] occurrences matches {0..1} matches {    -- Activity value
						value matches {
							C_DV_QUANTITY <
								property = <[openehr::380]>
								list = <
									["1"] = <
										units = <"1">
										magnitude = <|0.0..10.0|>
									>
								>
							>
						}
					}
				}
			}
			ELEMENT[at0080] occurrences matches {0..1} matches {    -- Overall activity score
				value matches {
					C_DV_QUANTITY <
						property = <[openehr::380]>
						list = <
							["1"] = <
								units = <"1">
								magnitude = <|0.0..10.0|>
							>
						>
					>
				}
			}
			CLUSTER[at0097] occurrences matches {0..1} matches {    -- Genomic region studied
				items cardinality matches {1..*; unordered} matches {
					ELEMENT[at0105] occurrences matches {0..*} matches {    -- Allele covered
						value matches {
							DV_TEXT matches {*}
						}
					}
					allow_archetype CLUSTER[at0102] occurrences matches {0..*} matches {    -- Genomic region
						include
							archetype_id/value matches {/openEHR-EHR-CLUSTER\.knowledge_base_reference\.v1/}
					}
				}
			}
			allow_archetype CLUSTER[at0096] occurrences matches {0..*} matches {    -- Variant finding detail
				include
					archetype_id/value matches {/openEHR-EHR-CLUSTER\.genomic_variant_result\.v1/}
			}
			allow_archetype CLUSTER[at0103] occurrences matches {0..*} matches {    -- Additional details
				include
					archetype_id/value matches {/.*/}
			}
		}
	}

ontology
	terminologies_available = <"SNOMED-CT","LOINC">
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Pharmacogenetic test result">
					description = <"Findings from pharmacogenetic testing of a single gene, used to predict how an individual may respond to specific medications.">
				>
				["at0003"] = <
					text = <"Gene symbol">
					description = <"The official gene symbol approved by the HGNC, which is a short abbreviated form of the gene name.">
					comment = <"Coding with an external terminology is preferred, where possible.

Preferably this should be coded with terms from the HGNC (HUGO Gene Nomenclature Committee) nomenclature, using 'http://www.genenames.org' as the terminology identifier.

For example: 'http://www.genenames.org::CYP2C9 | CYP2C9 |'.">
				>
				["at0004"] = <
					text = <"Diplotype">
					description = <"A representation of the diplotype as a coded term or text string, commonly including the gene symbol, gene change descriptors or star allele diplotypes.">
					comment = <"Currently there is no definitive standardised method of representing all diplotypes.">
				>
				["at0052"] = <
					text = <"Phenotype">
					description = <"The estimated pharmacological impact of the identified genotype.">
					comment = <"The coded text values are based on CPIC phenotype recommendations (see: https://www.ncbi.nlm.nih.gov/pmc/articles/PMC5253119/) with a small set of additional Aminoglycoside terms that are in part of CPIC guidance but have not yet been mapped to SNOMED or LOINC (see: https://files.cpicpgx.org/data/guideline/publication/aminoglycosides/2021/34032273.pdf).

It is recognised that this valueset may need extension in the future, or to allow local variation.

The free text choice permits use of other local text/coded text value sets, as required.

The free text choice permits use of other local text/coded text value sets, as required.">
				>
				["at0053"] = <
					text = <"Normal metaboliser">
					description = <"The metaboliser status is normal.">
				>
				["at0054"] = <
					text = <"Rapid metaboliser">
					description = <"The metaboliser status is rapid.">
				>
				["at0055"] = <
					text = <"Ultrarapid metaboliser">
					description = <"The metaboliser status is ultrarapid.">
				>
				["at0056"] = <
					text = <"Intermediate metaboliser">
					description = <"The metaboliser status is intermediate.">
				>
				["at0057"] = <
					text = <"Poor metaboliser">
					description = <"The metaboliser status is poor.">
				>
				["at0080"] = <
					text = <"Overall activity score">
					description = <"The overall activity score for this test result, derived from the total of each allele activity value.">
					comment = <"For example: '1.5'.">
				>
				["at0084"] = <
					text = <"Individual activity value">
					description = <"A list of enzyme activity values each associated with a specific allele, which are used to derive the Overall activity score.">
				>
				["at0085"] = <
					text = <"Activity value">
					description = <"An associated, derived activity value, based on known association with specific genotypes, which is required to assess metaboliser status for some phenotypes.">
					comment = <"For example: '0.5'.">
				>
				["at0086"] = <
					text = <"Allele haplotype">
					description = <"The allele haplotype descriptor associated with the activity value.">
					comment = <"For example: '*36'.">
				>
				["at0087"] = <
					text = <"Increased function">
					description = <"Functional activity is increased.">
				>
				["at0088"] = <
					text = <"Decreased function">
					description = <"Functional activity is decreased.">
				>
				["at0089"] = <
					text = <"Normal function">
					description = <"Functional activity is normal.">
				>
				["at0090"] = <
					text = <"Poor function">
					description = <"Functional activity is poor.">
				>
				["at0091"] = <
					text = <"Negative">
					description = <"Negative test for risk alleles.">
				>
				["at0092"] = <
					text = <"Positive">
					description = <"Positive test for risk alleles.">
				>
				["at0093"] = <
					text = <"Normal risk of aminoglycoside-induced hearing loss">
					description = <"Normal risk of aminoglycoside-induced hearing loss.">
				>
				["at0094"] = <
					text = <"Increased risk of aminoglycoside-induced hearing loss">
					description = <"Increased risk of aminoglycoside-induced hearing loss.">
				>
				["at0095"] = <
					text = <"Uncertain risk of aminoglycoside-induced hearing loss">
					description = <"Uncertain risk of aminoglycoside-induced hearing loss.">
				>
				["at0096"] = <
					text = <"Variant finding detail">
					description = <"Details of a single variant finding.">
					comment = <"It is recommended to use the openEHR-EHR-CLUSTER.genomic_variant_result.v1 archetype as the main container for detailed variant information, possibly making use of other structured variant archetypes or as an attached file in a recognised standard format, such as the GA4GH VRS format.">
				>
				["at0097"] = <
					text = <"Genomic region studied">
					description = <"A list of the genomic region(s) studied by this pharmacogenetic test. Typically, this would be a list of SNP rsNumbers but other variant or genomic region identifiers can be used.">
					comment = <"This element is significant to interpretation of the test, as individual pharmacogenetic tests may differ in their coverage of significant alleles/variants.">
				>
				["at0102"] = <
					text = <"Genomic region">
					description = <"An identifier or descriptor of a genomic region studied, typically an allele, but also a variant or other genomic region.">
					comment = <"It is recommended to use the openEHR-EHR-CLUSTER.knowledge_base_reference.v1 archetype to carry the SNP rsNumber or other genomic region identifier.

For example:

Knowledge base name: 'http://terminology.hl7.org/NamingSystem/v3-dbSNP'.

Item name: 'rs3918290'.">
				>
				["at0103"] = <
					text = <"Additional details">
					description = <"Structured details or questions about the pharmacogenetic test result.">
				>
				["at0105"] = <
					text = <"Allele covered">
					description = <"The haplotype description of an allele covered by the test.">
					comment = <"In pharmacogenetic test results, allele coverage is commonly described as a list of alleles, usually as 'star alleles'.
For example: '*3'.">
				>
			>
		>
	>
	term_binding = <
		["SNOMED-CT"] = <
			items = <
				["at0053"] = <[SNOMED-CT::3351000181105]>
				["at0054"] = <[SNOMED-CT::3341000181107]>
				["at0055"] = <[SNOMED-CT::3331000181100]>
				["at0056"] = <[SNOMED-CT::3361000181108]>
				["at0057"] = <[SNOMED-CT::3371000181103]>
				["at0087"] = <[SNOMED-CT::3381000181101]>
				["at0088"] = <[SNOMED-CT::3401000181101]>
				["at0089"] = <[SNOMED-CT::3391000181104]>
				["at0090"] = <[SNOMED-CT::3451000181100]>
				["at0091"] = <[SNOMED-CT::260385009]>
				["at0092"] = <[SNOMED-CT::10828004]>
			>
		>
		["LOINC"] = <
			items = <
				["at0053"] = <[LOINC::LA25391-6]>
				["at0054"] = <[LOINC::LA25390-8]>
				["at0055"] = <[LOINC::LA10315-2]>
				["at0056"] = <[LOINC::LA10317-8]>
				["at0057"] = <[LOINC::LA9657-3]>
				["at0087"] = <[LOINC::LA25392-4]>
				["at0088"] = <[LOINC::LA25395-7]>
				["at0089"] = <[LOINC::LA25393-2]>
				["at0090"] = <[LOINC::LA25394-0]>
				["at0091"] = <[LOINC::LA6577-6]>
				["at0092"] = <[LOINC::LA6576-8]>
			>
		>
	>
